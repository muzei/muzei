/*
 * Copyright 2014 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

dependencies {
    api libs.coroutines.core
    api libs.coroutines.android
    implementation project(':muzei-api')
    implementation project(':extensions')
    implementation libs.okhttp.core
    implementation libs.coil
    implementation libs.activity
    implementation libs.core
    implementation libs.fragment
    implementation libs.appCompat
    implementation libs.material
    implementation libs.recyclerView
    implementation libs.constraintLayout
    implementation libs.exifInterface
    implementation libs.lifecycle.common
    implementation libs.lifecycle.livedata
    implementation libs.room.ktx
    implementation libs.room.paging
    implementation libs.workManager
    ksp libs.room.compiler
    implementation libs.paging
}

android {
    namespace 'com.google.android.apps.muzei.gallery'
    compileSdk libs.versions.compileSdk.get().toInteger()

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion libs.versions.targetSdk.get()

        def galleryAuthorityValue = "com.google.android.apps.muzei.gallery"
        def galleryArtAuthorityValue = "com.google.android.apps.muzei.gallery.art"
        manifestPlaceholders = [galleryAuthority: galleryAuthorityValue,
                                galleryArtAuthority: galleryArtAuthorityValue]
        buildConfigField "String", "GALLERY_AUTHORITY", "\"${galleryAuthorityValue}\""
        buildConfigField "String", "GALLERY_ART_AUTHORITY", "\"${galleryArtAuthorityValue}\""

        multiDexEnabled true

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    buildTypes {
        publicBeta
        publicDebug
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}