/*
 * Copyright 2014 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'androidx.navigation.safeargs.kotlin'

project.archivesBaseName = "muzei"

android {
    namespace 'net.nurik.roman.muzei'
    compileSdk libs.versions.compileSdk.get().toInteger()

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('../version.properties')))

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "net.nurik.roman.muzei"
        minSdkVersion 21
        targetSdkVersion libs.versions.targetSdk.get()

        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()

        multiDexEnabled true
        wearAppUnbundled true

        buildConfigField "String", "DEVICE_TYPE", "\"android\""
    }

    signingConfigs {
        release {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            Properties keyProps = new Properties()
            if (localProps['keystore.props.file'] != null) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            }
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
            FirebasePerformance {
                instrumentationEnabled false
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            versionNameSuffix " " + versionProps['betaNumber']
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            renderscriptDebuggable true
            versionNameSuffix " Debug " + versionProps['betaNumber']
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

dependencies {
    api libs.coroutines.core
    api libs.coroutines.android
    api libs.coroutines.playservices
    implementation libs.multidex
    implementation libs.coil
    implementation libs.wearable.playservices
    implementation libs.tasker
    implementation libs.subsampling
    implementation libs.core
    implementation libs.fragment
    implementation libs.appCompat
    implementation libs.drawerLayout
    implementation libs.recyclerView
    implementation libs.swipeRefreshLayout
    implementation libs.viewPager2
    implementation libs.material
    implementation libs.browser
    implementation libs.exifInterface
    implementation libs.constraintLayout
    implementation libs.lifecycle.livedata
    implementation libs.lifecycle.common
    implementation libs.preference
    implementation libs.navigation.fragment
    implementation libs.navigation.ui

    implementation project(':muzei-api')
    implementation project(':extensions')
    implementation project(':legacy-common')
    implementation project(':android-client-common')
    implementation project(':source-featured-art')
    implementation project(':source-gallery')
    implementation project(':source-single')
    implementation project(':gl-wallpaper')
}

apply plugin: 'com.google.gms.google-services'